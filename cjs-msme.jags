
model{

#-- effets sur les parametres
for (k in 1:nsp){
	for (t in 1:(n.occasions-1)){
		logit(phi[k,t]) <-  mu.phi +
                         ( epsilon[t] 
                         *alpha[k] )
		for (s in 1:nsites[k]){
			p[k,vecsp[s,k],t] <- mu.p + beta[k] +slopeLsex[vecsp[s,k]]*beta_p_Sex
                          + eta[vecsp[s,k]] # le nb de sites differe par espece - LE CAUCHEMAR
		}
	}
}

for (k in 1:nsp){
	for (s in 1:nsites[k]){
		eta[vecsp[s,k]] ~ dnorm(0,tau.p) # effet aleatoire site sur detection
	}
}

for (k in 1:nsp){	
	alpha[k] ~ dunif(0,1) # effet fixe espece sur survie
	beta[k] ~ dnorm(0,tau.beta) # effet aleatoire espece sur detection
}

for (t in 1:(n.occasions-1)){
	epsilon[t] ~ dunif(0, 1) # effet aleatoire temps sur survie
}

beta_p_Sex ~ dunif(-10,10)#Sex difference in detection probability

#-- priors
#sigma.alpha ~ dunif(0, 5)                
#tau.alpha <- pow(sigma.alpha, -2)
#sigma2.alpha <- pow(sigma.alpha, 2)

sigma.beta ~ dunif(0, 5)                
tau.beta <- pow(sigma.beta, -2)
sigma2.beta <- pow(sigma.beta, 2)

mean.phi ~ dunif(0, 1)             
mu.phi <- log(mean.phi / (1-mean.phi)) 
sigma.phi ~ dunif(0, 5)                
tau.phi <- pow(sigma.phi, -2)
sigma2.phi <- pow(sigma.phi, 2)

mean.p ~ dunif(0, 1)	           
mu.p <- log(mean.p / (1-mean.p)) 
sigma.p ~ dunif(0, 5)                
tau.p <- pow(sigma.p, -2)
sigma2.p <- pow(sigma.p, 2)

#-- vraisemblance multinomiale espece x site
for (k in 1:nsp){ # boucle sur espece
	for (s in 1:nsites[k]){ # boucle sur site
		for (t in 1:(n.occasions-1)){ # boucle sur temps
			marr[t,1:n.occasions,vecsp[s,k]] ~ dmulti(pr[k,vecsp[s,k],t,], rel[t,vecsp[s,k]])
		}
		# Define the cell probabilities of the m-array:
		# Main diagonal
		for (t in 1:(n.occasions-1)){
			q[k,vecsp[s,k],t] <- 1-p[k,vecsp[s,k],t]
			pr[k,vecsp[s,k],t,t] <- phi[k,t]*p[k,vecsp[s,k],t]
			# Above main diagonal
			for (j in (t+1):(n.occasions-1)){
				pr[k,vecsp[s,k],t,j] <- prod(phi[k,t:j])*prod(q[k,vecsp[s,k],t:(j-1)])*p[k,vecsp[s,k],j]
			} #j
			# Below main diagonal
			for (j in 1:(t-1)){
				pr[k,vecsp[s,k],t,j]<-0
			} #j
		} #t
		# Last column: probability of non-recapture
		for (t in 1:(n.occasions-1)){
			pr[k,vecsp[s,k],t,n.occasions] <- 1-sum(pr[k,vecsp[s,k],t,1:(n.occasions-1)])
		} # t
	} # s
} # k
}

